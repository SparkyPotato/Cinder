find_package(Doxygen REQUIRED)

file(GLOB_RECURSE CODE_HEADERS CONFIGURE_DEPENDS
	${PROJECT_SOURCE_DIR}/API/*.h
)

set(DOXYGEN_INPUT_DIR ${PROJECT_SOURCE_DIR})
set(DOXYGEN_OUTPUT_DIR ${CMAKE_CURRENT_BINARY_DIR}/Docs/Doxygen)
set(DOXYGEN_INDEX_FILE ${DOXYGEN_OUTPUT_DIR}/html/index.html)

set(DOXYFILE_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(DOXYFILE_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

configure_file(${DOXYFILE_IN} ${DOXYFILE_OUT} @ONLY)

file(MAKE_DIRECTORY ${DOXYGEN_OUTPUT_DIR})
add_custom_command(OUTPUT ${DOXYGEN_INDEX_FILE}
	DEPENDS ${CODE_HEADERS}
	COMMAND ${CMAKE_COMMAND} -DDOXYGEN_EXECUTABLE=${DOXYGEN_EXECUTABLE} -P ${CMAKE_MODULE_PATH}/RunDoxygen.cmake
	MAIN_DEPENDENCY ${DOXYFILE_OUT} ${DOXYFILE_IN}
	COMMENT "Generating documentation with Doxygen."
)

add_custom_target(Doxygen ALL DEPENDS ${DOXYGEN_INDEX_FILE})

set(SPHINX_SOURCE ${CMAKE_CURRENT_SOURCE_DIR}/source)
set(SPHINX_BUILD ${CMAKE_CURRENT_BINARY_DIR}/Docs/Sphinx)
set(SPHINX_INDEX_FILE ${SPHINX_BUILD}/index.html)

file(GLOB_RECURSE SPHINX_FILES CONFIGURE_DEPENDS 
	${SPHINX_SOURCE}/*.rst
)

add_custom_command(OUTPUT ${SPHINX_INDEX_FILE}
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
	DEPENDS ${SPHINX_FILES}
	DEPENDS ${DOXYGEN_INDEX_FILE}
	COMMAND ${CMAKE_COMMAND} -DCMAKE_MODULE_PATH=${CMAKE_MODULE_PATH} -DSPHINX_SOURCE=${SPHINX_SOURCE} -P ${CMAKE_MODULE_PATH}/RunSphinx.cmake
	MAIN_DEPENDENCY ${SPHINX_SOURCE}/conf.py
	COMMENT "Generating documentation with Sphinx."
)

add_custom_target(Sphinx ALL DEPENDS ${SPHINX_INDEX_FILE})

include(GNUInstallDirs)
install(DIRECTORY ${SPHINX_BUILD}
DESTINATION ${CMAKE_INSTALL_DOCDIR})
